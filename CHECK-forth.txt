( CHECK                                     TF  19:30 07/21/87 ) 
( Last change:   Screen  001                TF  12:52 07/24/87 )  
                                                               
( Durchchecken der I/O-Platinen des Z80-Subsystems:            )
( =================================================            )
( - CPU-Karte  :  CTC, PIO1, PIO2                              )
( - PIO-Karte  :  PIO1...PIO4                                  )
( - FIFO-Karte :  FIO1...FIO3                                  )
( - Timerkarte :  CTC1...CTC4                                  ) 
                                                               
( Testmessprogramm:                                            )
( =================                                            )
( NMI-Puls erzeugt Interrupts fÅ¸r Laser-MOPS -> Laserscan      )
( EICHGATE steuert den EICHMARKEN-Z‰hler                       )
( FLUOGATE steuert den FluoreszenZ-Z‰hler                      )
                                                                
( Adressen-Belegungen                      TF  12:52 07/24/87  ) 
(                                                              )
( Die Adressen der :                                           )
(                                                              )
(                   PIO-Karte  - 060H                          )
(                   Timerkarte - 030H                          )
(                                                              )
( entsprechen der CP/M-Maschine von W.Kress & U.Urmoneit       )
( und sind somit gegeneinander direkt austauschbar.            )                                                               
( Screen 2, 3 und 4 sind Kommandoscreens fÅ¸r PC vom SLAVE      )                                                                   
3 20 THRU                                                                                                                                          
( MM : PC-Kommandoscreen                      KD 15:54 07/20/87)
( ACHTUNG:  Dieser Screen kann  n u r  auf dem PC auf          )
( ========  Veranlassung des SLAVE ausgefÅ¸hrt werden !!!       )                                                               
( Initialisiere Screenz‰hler )                                  
1.SCR                                                                                                                           
( Benutze Messdatei MESSDATF.SCR f¸År Fluoreszenzmessungen      )
USING MESSDATF                                                         
                                      
( IO-Adressen: PIO-Karte                      TF 12:43 07/24/87)
(              PIO1, PIO2  -  Base Address : 060H              )
HEX 
                                                                                                
: EQU CONSTANT ;                                                                                                                
060          EQU     IOB1A_D    ( PIO1: Port-A Data  )          
1 IOB1A_D +  EQU     IOB1A_C    (       Port-A Ctrl  )          
2 IOB1A_D +  EQU     IOB1B_D    (       Port-B Data  )          
3 IOB1A_D +  EQU     IOB1B_C    (       Port-B Ctrl  )                                          
4 IOB1A_D +  EQU     IOB2A_D    ( PIO2: Port-A Data  )          
5 IOB1A_D +  EQU     IOB2A_C    (       Port-A Ctrl  )          
6 IOB1A_D +  EQU     IOB2B_D    (       Port-B Data  )          
7 IOB1A_D +  EQU     IOB2B_C    (       Port-B Ctrl  ) 
                                                                     
( IO-Adressen: PIO-Karte   PIO3, PIO4         TF 15:51 07/04/87)   
                                                                  
HEX                                                                                                                            
8 IOB1A_D +  EQU     IOB3A_D    ( PIO3: Port-A Data  )          
9 IOB1A_D +  EQU     IOB3A_C    (       Port-A Ctrl  )          
A IOB1A_D +  EQU     IOB3B_D    (       Port-B Data  )          
B IOB1A_D +  EQU     IOB3B_C    (       Port-B Ctrl  )                                                    
C IOB1A_D +  EQU     IOB4A_D    ( PIO4: Port-A Data  )          
D IOB1A_D +  EQU     IOB4A_C    (       Port-A Ctrl  )          
E IOB1A_D +  EQU     IOB4B_D    (       Port-B Data  )          
F IOB1A_D +  EQU     IOB4B_C    (       Port-B Ctrl  )      
                                                                                                                                                                                               
( Init I/O-Bus :   INIT_IOB1                  TF 15:44 07/04/87)
( DATA_IN, DATA_OUT                                            )
BINARY                                                                                                         
: INIT_IOB1                                                                                                                         
  11001111 IOB1A_C P!    ( PIO1 in mode 3 betreiben )             
  11111111 IOB1A_C P!    ( Alle Bits INPUT )                      
  11001111 IOB1B_C P!                                             
  11111111 IOB1B_C P!                                                                             
  11001111 IOB2A_C P!    ( PIO2 in mode 3 betreiben )             
  00000000 IOB2A_C P!    ( Alle Bits OUTPUT )                    
  11001111 IOB2B_C P!                                            
  00000000 IOB2B_C P!                                      
;                                                               
( Init I/O-Bus  :  INIT_IOB2                  TF 15:44 07/04/87)
( ADDRESS, CONTROL                                             )                                                                
: INIT_IOB2                                                                                       
  11001111 IOB3A_C P!    ( PIO3 in mode 3 betreiben )             
  00000000 IOB3A_C P!    ( Alle Bits OUTPUT )                     
  11001111 IOB3B_C P!                                             
  00000000 IOB3B_C P!                                                                             
  11001111 IOB3A_C P!    ( PIO4 in mode 3 betreiben )             
  00000000 IOB3A_C P!    ( Alle Bits OUTPUT )                    
  11001111 IOB3B_C P!                                            
  00000000 IOB3B_C P!                                      
;     
                                                                   
                                                       
( Z‰hlerroutinen  CLR, LATCH                  KD 11:47 07/21/87)
HEX                                                              
                                                               
IOB3B_D  EQU  CTRL       ( CONTROL-Port des IOB-Busses )          
                                                              
( CTRL0 <->  CLEAR1   :  Lˆsche Z‰hler Fluoreszenz             )
( CTRL1 <->  CLEAR2   :  Lˆsche Z‰hler Eichmarken              )
( CTRL2 <->  LATCH1   :  ¸bernehme Z‰hler Fluoreszenz          )
( CTRL3 <->  LATCH2   :  ¸bernehme Z‰hler Eichmarken           ) 
                                                               
: CLR1    01 CTRL P! 0 CTRL P! ;                                
: CLR2    02 CTRL P! 0 CTRL P! ;                                 
                                                               
: LATCH1  04 CTRL P! 0 CTRL P! ;                                
: LATCH2  08 CTRL P! 0 CTRL P! ;                                                                
                                
( Z‰hler-Adressierung                      TF TF 08:47 07/23/87)
                                                                
IOB3A_D     EQU    ADR           ( ADDRESS-Port des IOB-Bus )                                                                   
: DISABLE   0 ADR P! ;                                           
: EN_LOW1  10 ADR P! DISABLE ;       ( FLUO-Z‰hler auslesen )   
: EN_HIGH1 11 ADR P! DISABLE ;       (       "         "    )   
: EN_LOW2  12 ADR P! DISABLE ;       ( EICH-Z‰hler     "    )   
: EN_HIGH2 13 ADR P! DISABLE ;       (       "         "    )                                                                   
( --  n  )                                                      
: DATA_IN  
  IOB1A_D P@ 
  IOB1B_D P@     ( Einlesen DATA-IN Bus ) 
  100 * + ;      ( 16 Bit-Wert  n       )
                                                     
                                                                              
( Z‰hler-Auslesen   COUNTER                TF TF 12:45 07/24/87)
                                                                
( Auslesen des FLUORESZENZ-Z‰hlers : 16 Bit )                   
( --  n  )                                                      
: FCOUNTER LATCH1                                             
  EN_LOW1  DATA_IN                                                
  CLR1 ;                                                           
                                                   
( Auslesen des EICHMARKEN-Z‰hlers : 16 Bit )                    
( --  n )                                                       
: ECOUNTER  LATCH2                                                            
  EN_LOW2  DATA_IN                                                
  CLR2 ;                                                               
                                                                 
                                             
( INIT der ECB-Karten                      TF TF 12:46 07/24/87)
HEX                                                             
                                                               
: INIT_IOB                                                     
  FF ADR     P!  0 ADR     P!  ( Adressbus Nullen  )              
  FF CTRL    P!  0 CTRL    P!  ( Controlbus Nullen )             
  FF IOB2A_D P!  0 IOB2A_D P!  ( DATA-OUT Bus  "   )             
  FF IOB2B_D P!  0 IOB2B_D P!                                     
  CLR1 CLR2 ;                  ( Z‰hler lˆschen    )            
                                                          
: INIT_SUB                                                              
  INIT_IOB1  INIT_IOB2   ( Interface-Bus initialisieren  )        
  INIT_IOB ;             ( IOB feste Werte zuordnen      )
                                                                                                                                                                     
( VAR,CONST                                TF KD 18:23 07/21/87)
DECIMAL                                                         

1024 10 *  CONSTANT  EI     ( EICHBUF = 10 kB )                   
                                           
                   
( CREATE KOMMBUF 1024 ALLOT  1 kB Screenbuffer fÅ¸r Kommentar   ) 
( CREATE FLUOBUF 1024 ALLOT  1 kB Screenbuffer f¸År Fluowerte   )
( CREATE EICHBUF EI   ALLOT  10 kB Array fÅ¸r Eichmarken        )                                                                
0     VARIABLE  ZFLUO    ( Z‰hlvariable FLUO )                  
0     VARIABLE  ZEICH    ( Z‰hlvariable EICH )           
                                                                      
HEX                                                             
9000 CONSTANT KOMMBUF                                           
9800 CONSTANT FLUOBUF                                           
A000 CONSTANT EICHBUF                                                        
                                                   
( E>ASC, F>ASC                             TF TF 12:46 07/24/87)  
                                                              
( 16 Bit -> 5 stellige ASCII-Umwandlung EICH )                  
( n  ---   )                                                    
:  E>ASC  0 <# # # # # # #> ;                                    
                                                               
( 16 Bit -> 9 stellige ASCII-Umwandlung FLUO )                  
(  n  ---   )                                                   
:  F>ASC  0 <# # # # # # # # # # #> ;                                              
                                                                                                                                            
( KOM, LS_*                                TF  17:33 07/21/87 ) 
HEX                                                             
( zeile - )                                                     
: KOM  40 * KOMMBUF + 40 EXPECT ; ( Eingabe -> Komm.buffer )
                                                                    
: BUFK  KOMMBUF 400 DUMP ;     ( Kommentar Dumpen )             
: BUFM  FLUOBUF 400 DUMP ;     ( Messdaten DUMPen )             
: BUFE  EICHBUF 400 DUMP ;     ( Eichdaten DUMPen )             
: KCLR  KOMMBUF 400 20 FILL ;  ( Lˆschen )                      
: FCLR  FLUOBUF 400 20 FILL ;  ( Lˆschen )                      
: ECLR  EICHBUF EI  20 FILL ;  ( Lˆschen )                                       
                                              
DECIMAL                                                        
: KOMMENTAR  ." Kommentar zur Messung bitte eingeben:" CR                    
  6 0 DO CR I DUP . ." Zeile: " KOM LOOP ;                       
                                                   
( FSAVE, ESAVE                             TF  08:59 07/23/87 ) 
DECIMAL                                                         
( FLUO-Z‰hlraten -> Screenbuffer 24 Bit )                      
: FSAVE  FCOUNTER F>ASC ZFLUO @ SWAP CMOVE ;                                                                                    
( EICH-Z‰hlraten -> Array EICHBUF 16 Bit )                      
: ESAVE  ECOUNTER DROP ZEICH @ ! ;   ( High-16 Bit weglassen )                                  
: ZF  ZFLUO @ 10 + ZFLUO ! ; ( Fluoz‰hler incrementieren )      
: ZE  ZEICH @ 1+   ZEICH ! ; ( Eichz‰hler incrementieren )                                
                                      
: GETF FSAVE ZF ;  ( FLUO-Z‰hlrate holen, speichern, Var incr. )
: GETE ESAVE ZE ;  ( EICH-Z‰hlrate holen, speichern, Var incr. )
                                                                
: MAKEF FLUOBUF ZFLUO ! ;  ( Fluo‰hler-Zuweisung )             
: MAKEE EICHBUF ZEICH ! ;  ( Eichz‰hler-Zuweisung )             

( MASSENSPEICHER                           TF TF 12:47 07/24/87)
HEX                                                             
: UPFL    UPDATE FLUSH ;                                        
: LS_KOM  9 BLOCK KOMMBUF SWAP 400 CMOVE UPFL ; ( KOMM -> # 9  )
: LS_DAT  A BLOCK FLUOBUF SWAP 400 CMOVE UPFL ; ( FLUO -> # 10 )   
                                                             
( PC-Kommandoscreens: bei Aufruf dieser Worte auf dem SLAVE   ) 
( reagiert der PC entsprechend und fÅ¸hrt die Befehle aus, die ) 
( in den Screens der durch USING angemeldeten Datei stehen.   )                                           

( MESSDATF.SCR laden )                                          
: MM  CR ." FLUO-Datei:" 2  MS_R_LOAD ;                                                                                         
( screen# - )                                                   
: DO.PC  MS_R_LOAD ;  ( Bezieht sich auf MESSDATF.SCR )                                                                         
( MESSE                                    TF TF 12:46 07/24/87)
DECIMAL                                                                                         
: TRANSFERM  ."    Messdaten  -> PC"  LS_DAT  CR ;               
: TRANSFERK  ."    Kommentar  -> PC"  LS_KOM  CR ;               
: TRANSFERE  ."    Eichmarken -> PC"          CR ;                                              
: CLEARBUF FCLR KCLR ;                                                                                                                                                                                                                                                                  
( NMI, GATE-Steuerung                      TF  11:01 07/23/87  ) 
HEX                                                             
( CTRL4 <->  NMI      :  NMI-Impuls f¸År MOPS                   )
( CTRL5 <->  EICHGATE :  Z‰hlergate fÅ¸r Eichmarken             )
( CTRL6 <->  FLUOGATE :  Z‰hlergate fÅ¸r Fluoreszenz            )
( CTRL7 <->  frei                                              )
: NMI1    10 CTRL P! ;                 ( NMI auf 1 setzen )     
: NMI      0 CTRL P! 10 CTRL P! ;      ( ca. 77 us-Puls )                                                         
: EICHGATE 20 CTRL P!                                                      
  FF 0 DO LOOP       ( ca. 10 ms )                                
  0 CTRL P! ;                                                      
                                                   
: FLUOGATE 40 CTRL P!                                                      
  A8C 0 DO LOOP      ( ca. 100 ms , A8Ch = 2700 )                 
  0 CTRL P! ;                                         

( IMPULSE                                     TF 12:49 07/24/87)
( Pr¸Åfung des Impulsmusters NMI, EICHGATE, FLUOGATE            )
                                                                
: IMPULSE                                                             
  25 0 DO        ( 25 Zyklen † 100 Fluopunkte  )              
  100 0 DO       ( 100 Fluopunkte f¸År 1 Screen )                 
  4 0 DO                                                           
  NMI EICHGATE   ( 4x Eichmarken )                               
  LOOP                                                            
  FLUOGATE       ( 1x Fluopunkte )                             
  LOOP                                                       
  LOOP  ;
                                                       
: EG EICHGATE NMI NMI ;                                   
                                                                     
( MESSUNG                                  TF TF 12:50 07/24/87)
DECIMAL  ( n  - )                                               
: MESSUNG DECIMAL CLEARBUF MM MAKEE CR                      
             
  0 DO                   ( n  Zyklen † 100 Fluopunkte  )             
  MAKEF                  ( Fluoscreenz‰hler initialisieren )         
  I 1+  . ." Zyklus : "                                          
  100 0 DO               ( 100 Fluopunkte f¸År 1 Screen )                 
  NMI EG EG EG EICHGATE NMI                                      
  FLUOGATE GETF          ( 1x Fluopunkte )               
  LOOP                                                            
  TRANSFERM              ( Messdatentransfer -> PC )              
  1 DO.PC                ( Kopieren der Screens   )               
  FCLR                   ( Fluo-Puffer lˆschen    )          
  LOOP  ;                                                        
                                                                                                                          
( Starte MESSUNG                           TF TF 12:48 07/24/87)
                                                                
( Initialiere IOB-Bus und NMI=1, active low )                   
: INIT INIT_SUB NMI1 ;      
   
( Anzahl der Screens bitte eingeben:   1 Screen = 0.1 GHz )     
( n - )                                                         
: TM CR ." F l u o m e s s u n g " CR CR                            
  INIT ." Bitte  JETZT  den NMI-Eingang beschalten -> "          
  KEY DROP                                                       
  MESSUNG                                                        
  KOMMENTAR TRANSFERK                                            
  CR ." Ende" CR ;   

